buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = "1.0.1"
group = "com.focamacho.sealmixins"
archivesBaseName = "SealMixins-1.12.2"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

sourceSets {
    main {
        ext.refMap = 'mixins.sealmixins.refmap.json'
    }
}

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "Focamacho's Repositry"
        url = "https://repo.focamacho.com/repository/releases/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.16.5-36.1.16"

	//Immersive Engineering
    compileOnly("curse.maven:immersiveengineering-231951:2974106")

    //Tech Guns
    compileOnly("curse.maven:techguns-244201:2958103")

    //Mekanism
    compileOnly("curse.maven:mekanism-268560:2835175")

    //Patchouli
    compileOnly("curse.maven:patchouli-306770:3087124")

    //The Aurorian
    compileOnly("curse.maven:theaurorian-352137:3249263")

    //Actually Additions
    compileOnly("curse.maven:actuallyadditions-228404:3117927")

    //EnderIO
    compileOnly("curse.maven:enderio-64578:3124452")

    //Draconic Evolution
    compileOnly("curse.maven:draconicevolution-223565:3051542")

    //Tinkers' Construct
    compileOnly("curse.maven:tconstruct-74072:2902483")

    //Mixins
    embed("org.spongepowered:mixin:0.8-SNAPSHOT") {
        transitive = false
    }

    //Seal Config
    embed("com.focamacho:sealconfig:+")
}

jar {
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'LICENSE.txt', 'META-INF/MANIFSET.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF'
    }

    manifest.attributes(
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.sealmixins.json',
            'FMLCorePlugin': 'com.focamacho.sealmixins.asm.SealMixinsLoader'
    )
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}